import org.apache.tools.ant.taskdefs.condition.Os

ext {
	// The drivers we want to use
	drivers_every = [
		"firefox",
		"chrome",
		"phantomJs",
	]
	drivers_win8 = [
		"ie",
	]
	drivers_win10 = [
		"ie",
		"edge",
	]
	drivers_mac = [
		"safari",
	]

	drivers_all = drivers_every + drivers_win8 + drivers_win10 + drivers_mac
	drivers_all.unique()

	ext {
		groovyVersion = '2.4.7'
		gebVersion = '1.0'
		seleniumVersion = '3.0.1'
		geckoDriverVersion = '0.11.1'
		chromeDriverVersion = '2.25'
		phantomJsVersion = '2.11'
		ieDriverVersion = '3.0.0'
		edgeDriverVersion = '3/2/D/32D3E464-F2EF-490F-841B-05D53C848D15'
	}
}

apply plugin: "groovy"
apply plugin: "eclipse"
apply from: "gradle/idea.gradle"
apply from: "gradle/osSpecificDownloads.gradle"

repositories { mavenCentral() }

dependencies {
	// If using Spock, need to depend on geb-spock
	testCompile "org.gebish:geb-spock:$gebVersion"
	testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-3") { exclude group: "org.codehaus.groovy" }
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"

	// If using JUnit, need to depend on geb-junit (3 or 4)
	testCompile "org.gebish:geb-junit4:$gebVersion"

	// Drivers
	testCompile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
	testCompile("com.codeborne:phantomjsdriver:1.3.0") { // phantomjs driver pulls in a different selenium version
		transitive = false }
}

task wrapper(type: Wrapper) { gradleVersion = '3.2' }

drivers_all.each { driver ->
	task "${driver}Test"(type: Test) {
		reports {
			html.destination = reporting.file("$name/tests")
			junitXml.destination = file("$buildDir/test-results/$name")
		}

		outputs.upToDateWhen { false }  // Always run tests

		systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
		systemProperty "geb.env", driver


		// '-Ptarget'にてテスト対象を指定します。
		if(project.hasProperty('target')){
			if( project.getAt('target') !="*" ){
				include '**/'+ project.getAt('target') + '.class'
			} else {
				// すべてをテスト対象にする場合やテスト対象を指定しない場合は、
				// テストケースが2回実行されてしまうためSuiteClassesを除外します。
				exclude '**/*Suite.class'
			}
		} else {
			exclude '**/*Suite.class'
		}



		// If you wanted to set the baseUrl in your build…
		// systemProperty "geb.build.baseUrl", "http://myapp.com"
		//systemProperty "geb.build.baseUrl", System.properties['geb.build.baseUrl']
	}
}

ieTest {
	dependsOn unzipIeDriver

	def iedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "IEDriverServer.exe" : ""
	systemProperty "webdriver.ie.driver", new File(unzipIeDriver.outputs.files.singleFile, iedriverFilename).absolutePath
}

edgeTest {
	dependsOn downloadEdgeDriver

	systemProperty "webdriver.edge.driver", new File(downloadEdgeDriver.outputs.files.singleFile, "").absolutePath
}

firefoxTest {
	dependsOn unzipGeckoDriver

	def geckodriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "geckodriver.exe" : "geckodriver"
	systemProperty "webdriver.gecko.driver", new File(unzipGeckoDriver.outputs.files.singleFile, geckodriverFilename).absolutePath
}

chromeTest {
	dependsOn unzipChromeDriver

	def chromedriverFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "chromedriver.exe" : "chromedriver"
	systemProperty "webdriver.chrome.driver", new File(unzipChromeDriver.outputs.files.singleFile, chromedriverFilename).absolutePath
}

phantomJsTest {
	dependsOn unzipPhantomJs

	def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "bin/phantomjs.exe" : "bin/phantomjs"
	systemProperty "phantomjs.binary.path", new File(unzipPhantomJs.outputs.files.singleFile, phantomJsFilename).absolutePath
}

test {
	dependsOn drivers_every.collect { tasks["${it}Test"] }

	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		// Windowsの場合、Windows 10かそれ以外で対象ブラウザを切り替え
		if (Os.isName("Windows 10")) {
			dependsOn drivers_win10.collect { tasks["${it}Test"] }
		} else {
			dependsOn drivers_win8.collect { tasks["${it}Test"] }
		}
	} else if (Os.isFamily(Os.FAMILY_MAC)) {
		// TODO:★★★★★★★★まだsafariのドライバーダウンロードは書いてない
		dependsOn drivers_mac.collect { tasks["${it}Test"] }
	}
	enabled = false
}

apply from: "gradle/ci.gradle"


// テストをクラス単位で並列実行させるプロセス上限数
//test.maxParallelForks = 5
